<?xml version="1.0" encoding="utf-8"?>
<!--- 

Minimal, UI-agnostic Preset Manager UI, consuming Rekal's Manager API 

-->
<s:Group
    xmlns:fx="http://ns.adobe.com/mxml/2009"
    xmlns:s="library://ns.adobe.com/flex/spark"
    xmlns:mx="library://ns.adobe.com/flex/mx">

    <fx:Metadata>
        [Style(name="headerStyle", type="String", inherit="no")]
        [Style(name="footerStyle", type="String", inherit="no")]
        [Style(name="comboStyle", type="String", inherit="no")]
        [Style(name="saveButtonStyle", type="String", inherit="no")]
        [Style(name="deleteButtonStyle", type="String", inherit="no")]
        
        [Style(name="vGap", type="Number", inherit="no")]
        [Style(name="hGap", type="Number", inherit="no")]
        
        [Style(name="paddingLeft", type="Number", inherit="no")]
        [Style(name="paddingTop", type="Number", inherit="no")]
        [Style(name="paddingRight", type="Number", inherit="no")]
        [Style(name="paddingBottom", type="Number", inherit="no")]
        [Style(name="padding", type="Number", inherit="no")]
    </fx:Metadata>

    <fx:Script>
        <![CDATA[
            import com.github.ciacob.asrekallibrary.Manager;
            import com.github.ciacob.asrekallibrary.Preset;
            import com.github.ciacob.asrekallibrary.events.PresetEvent;
            import spark.components.ComboBox;
            import mx.collections.ArrayCollection;
            
            /**
             * Set to `true` to have current selection preserved past a Save operation.
             * This is the default behavior.
             */
            [Bindable]
            public var keepSelection:Boolean = true;
            
            /**
             * Set to `true` to have the built-in header shown. This is the default
             * behavior. Set to `false` if you intend to use your own header instead.
             */
            [Bindable]
            public var showHeader:Boolean = true;
            
            /**
             * Asset(s) to populate the Delete button with, such as a String to use as
             * label, or an FXG class to use as an icon (if the button uses the
             * com.github.ciacob.flex.ui.skins.IconButtonSkin).
             * Note: you can set a different button skin via `deleteButtonStyle`.
             */
            [Bindable]
            public var deleteButtonContent:* = "Delete";
            
            /**
             * Asset(s) to populate the Save button with, such as a String to use as
             * label, or an FXG class to use as an icon (if the button uses the
             * com.github.ciacob.flex.ui.skins.IconButtonSkin).
             * Note: you can set a different button skin via `saveButtonStyle`.
             */
            [Bindable]
            public var saveButtonContent:* = "Save";
            
            /**
             * Text to use as the ComboBox prompt displayed when there is no current
             * item (either existing or custom).
             */
            [Bindable]
            public var promptText:String = "Type and press Enter...";
            
            /**
             * Text to use as the component's header, provided it is visible.
             */
            [Bindable]
            public var headerText:String = "Presets";
            
            /**
             * Time before status message clears itself, in seconds. Set to 0 to disable.
             */
            [Bindable]
            public var statusTimeOut:int = 3;
            
            /**
             * Optional function to pass to the Manager when retrieving the list of
             * Presets. If not provided, Presets are grouped by read-only status and
             * alphabetically sorted.
             */
            [Bindable]
            public var sortingFunction:Function;
            
            // The `maxWidth` property is rerouted to affect the container of the
            // ComboBox. It has a default value of 300 pixels.
            [Bindable]
            private var _maxContainerWidth:Number = 300;
            
            // The ComboBox data provider.
            [Bindable]
            private var _presets:ArrayCollection = new ArrayCollection;
            
            // The text to display underneath the ComboBox.
            [Bindable]
            private var _statusMsg:String = "";
            
            // The maximum number of chars that can be typed in the ComboBox.
            // This is only to discourage impractically long preset names, The Manager
            // can accommodate any Preset name.
            [Bindable]
            private var _numChars:uint = 40;
            
            // The cached storage of the Presets as returned by the Manager.
            private var _rawPresets:Vector.<Preset> = new Vector.<Preset>;
            
            // The underlying instance of the Manager class that provides preset
            // management services, as set via `initializeWith()`.
            private var _manager:Manager;
            
            // A Preset that needs to be set as current in the ComboBox past a
            // refresh operation (e.g., after Save).
            private var _presetToPersist:Preset;
            
            // The Timer instance used to discard the status message after a period
            // of idleness.
            private var _statusTimer:Timer;
            
            /**
             * Connects this UI component to a Manager instance. Connection is "hot",
             * meaning that the changes you operate on the Manager instance directly
             * will be reflected here in due time.
             *
             * Note: you MUST connect to a Manager in order to be able to use this
             * component.
             */
            public function initializeWith(manager:Manager):void {
                this._manager = manager;
                if (!_manager) {
                    return;
                };
                this._numChars = manager.labelMaxLength;
                _updateList();
            }
            
            /**
             * Sets the maximum width the ComboBox, buttons and the status message can take,
             * in pixels.
             */
            override public function set maxWidth(value:Number):void {
                _maxContainerWidth = value;
            }
            
            /**
             * Returns the maximum width the ComboBox, buttons and the status message can take,
             * in pixels. Default value is 300 pixels.
             */
            override public function get maxWidth():Number {
                return _maxContainerWidth;
            }
            
            /**
             * Sets status with a delay for auto-clearing itself.
             * @private
             */
            private function _setStatus(msg:String):void {
                _statusMsg = msg;
                if (!statusTimeOut) {
                    return;
                }
                const onTimerComplete:Function = function():void {
                    _statusMsg = "";
                    _statusTimer.removeEventListener(TimerEvent.TIMER_COMPLETE, onTimerComplete);
                    _statusTimer = null;
                };
                if (_statusTimer) {
                    _statusTimer.reset();
                }
                else {
                    _statusTimer = new Timer(statusTimeOut * 1000, 1);
                    _statusTimer.addEventListener(TimerEvent.TIMER_COMPLETE, onTimerComplete);
                }
                _statusTimer.start();
            }
            
            /**
             * Causes buttons to become available based on specific criteria.
             * @private
             */
            private function _updateButtons():void {
                const canWrite:Boolean = (presetList.selectedItem && !presetList.selectedItem.readonly);
                const exists:Boolean = (presetList.selectedIndex > ComboBox.CUSTOM_SELECTED_ITEM);
                deleteButton.enabled = (exists && canWrite);
                saveButton.enabled = canWrite;
            }
            
            /**
             * Helper, trims a string. Returns an empty string for `null`.
             * @private
             */
            private function _trim(str:String):String {
                if (!str) {
                    return '';
                }
                return str.replace(/^\s+|\s+$/g, '');
            }
            
            /**
             * Helper, sorts Presets. Places them in two groups, first read-only and then read-write.
             * Within each group, presets are alphabetically sorted.
             * @private
             */
            private function _defaultSort(a:Preset, b:Preset):int {
                const byName:int = a.name.localeCompare(b.name);
                return (b.readonly as int) - (a.readonly as int) || byName;
            }
            
            /**
             * Causes the list to be asynchronously rebuilt, picking up all changes, and
             * optionally restoring previous selection.
             * @private
             */
            private function _updateList():void {
                if (!_manager) {
                    return;
                };
                _manager.addEventListener(PresetEvent.LIST_COMPLETE, _onListComplete);
                _manager.listAsync(null, sortingFunction || _defaultSort);
            }
            
            /**
             * Executed when the manager finished its asynchronous listing and we can start
             * repopulating the ComboBox. We cache received data to use it in synchronous
             * local lookups.
             * @private
             */
            private function _onListComplete(e:PresetEvent):void {
                _manager.removeEventListener(PresetEvent.LIST_COMPLETE, _onListComplete);
                _presets.removeAll();
                _rawPresets = (e.data as Vector.<Preset>);
                if (_rawPresets) {
                    for (var i:int = 0; i < _rawPresets.length; i++) {
                        const preset:Preset = _rawPresets[i];
                        _presets.addItem(preset);
                    }
            
                    if (_presetToPersist && keepSelection) {
                        presetList.selectedItem = _presetToPersist;
                        _presetToPersist = null;
                        _updateButtons();
                    }
                }
            }
            
            /**
             * Executed when user clicks "Save".
             * @private
             */
            private function _onSaveClick():void {
                if (!_manager || !presetList.selectedItem) {
                    return;
                }
            
                // A `PresetEvent.SAVING` event is dispatched when user clicks "Save". Its `data` is an Object
                // with a `commit()` closure. Client code must listen for this event and call the closure,
                // passing it the data to save (see `$set()` in class Manager for supported data types).
                dispatchEvent(new PresetEvent(PresetEvent.SAVING, {
                                commit: function(dataToSave:Object):void {
                                    if (!dataToSave) {
                                        return;
                                    }
                                    _manager.addEventListener(PresetEvent.SET_COMPLETE, _onSaved);
                                    _manager.$setAsync(dataToSave, presetList.selectedItem.name, true);
                                }
                            }, true));
            }
            
            /**
             * Executed when the Manager has finished asynchronously storing client-provided data.
             * @private
             */
            private function _onSaved(e:PresetEvent):void {
            
                // Safeguard (should not be the case)
                if (!e || !e.data || !e.data.hasOwnProperty('code') || !e.data.preset) {
                    _setStatus("Internal error while saving.");
                    return;
                }
            
                const data:Object = e.data;
                const code:int = data.code;
                const preset:Preset = data.preset;
                const name:String = preset.name;
                _presetToPersist = preset;
            
                _manager.removeEventListener(PresetEvent.SET_COMPLETE, _onSaved);
            
                switch (code) {
                    case 1:
                        _setStatus("Preset saved: " + name);
                        _updateList();
                        break;
                    case 0:
                        _setStatus("Identical preset exists: " + name);
                        break;
                    case 2:
                        _setStatus("Updated preset: " + name);
                        _updateList();
                        break;
                }
            }
            
            /**
             * Executed when user clicks "Delete".
             * @private
             */
            private function _onDeleteClick():void {
                if (!_manager || !presetList.selectedItem || presetList.selectedItem.readonly) {
                    return;
                }
                _manager.addEventListener(PresetEvent.DELETE_COMPLETE, _onDelOpExecuted);
                _manager.$deleteAsync(presetList.selectedItem);
            }
            
            /**
             * Executed when manager finished asynchronously deleting given Preset.
             * @private
             */
            private function _onDelOpExecuted(e:PresetEvent):void {
                _manager.removeEventListener(PresetEvent.DELETE_COMPLETE, _onDelOpExecuted);
                if (e.data && e.data.code === 1) {
                    _setStatus("Deleted preset: " + e.data.preset.name);
                    _updateList();
                    return;
                }
            
                // Safeguard (should not be the case)
                _setStatus("Preset not found.");
            }
            
            /**
             * Executed when user causes the currently selected Preset to change, either via
             * mouse or keyboard. Sends to the outer world the current Preset.
             * @private
             */
            private function _onPresetChanged():void {
                if (!_manager) {
                    return;
                };
                if (presetList.selectedItem) {
            
                    // We only notify the outer world when an *existing* Preset has been selected.
                    if (presetList.selectedIndex >= 0) {
                        dispatchEvent(new PresetEvent(PresetEvent.CHANGE, presetList.selectedItem, true));
                        _setStatus('Applied preset: ' + presetList.selectedItem.name);
                    }
            
                    // UX sugar: assist users in creating new presets
                    if (presetList.selectedIndex === ComboBox.CUSTOM_SELECTED_ITEM) {
                        _setStatus('Hint: click Save to create this preset.');
                    }
                }
            
                _updateButtons();
            }
            
            /**
             * Called by the ComboBox component when it has been committed a free text that it
             * cannot resolve to an existing Preset. We transparently resolve the text to a Preset
             * and return it (to be later subjected to `_onPresetChanged`).
             * @private
             */
            private function _labelToItem(input:String):* {
                if (!_manager) {
                    return;
                };
            
                // The manager errors on empty preset names, so we stop this short.
                input = _trim(input);
                if (!input) {
                    return null;
                }
            
                // Safeguard (should not be the case)
                const existingPreset:Preset = _manager.lookup(input, _rawPresets);
            
                return existingPreset || new Preset(input);
            }
        ]]>
    </fx:Script>

    <!-- Outer container -->
    <s:VGroup width="100%"
        id="outerContainer"
        maxWidth="{_maxContainerWidth}"
        gap="{getStyle('vGap') || 0}"
        padding="{getStyle('padding') || 0}"
        paddingLeft="{getStyle('paddingLeft') || 0}"
        paddingRight="{getStyle('paddingRight') || 0}"
        paddingTop="{getStyle('paddingTop') || 0}"
        paddingBottom="{getStyle('paddingBottom') || 0}">

        <!-- Built-in optional header -->
        <s:Label text="{headerText}:"
            styleName="{getStyle('headerStyle')}"
            visible="{showHeader}"
            includeInLayout="{showHeader}"/>

        <!-- ComboBox and buttons -->
        <s:HGroup width="100%"
            gap="{getStyle('hGap') || 0}">

            <!-- Searchable list of Presets (and also a place to provide names for new Presets) -->
            <s:ComboBox width="100%"
                styleName="{getStyle('comboStyle')}"
                id="presetList"
                maxChars="{_numChars}"
                prompt="{promptText}"
                dataProvider="{_presets}"
                labelField="name"
                labelToItemFunction="{_labelToItem}"
                change="_onPresetChanged()"/>

            <!-- Button to save/update Presets -->
            <s:Button content="{saveButtonContent}"
                id="saveButton"
                styleName="{getStyle('saveButtonStyle')}"
                enabled="false"
                click="_onSaveClick()"/>

            <!-- Button to delete Presets -->
            <s:Button content="{deleteButtonContent}"
                id="deleteButton"
                styleName="{getStyle('deleteButtonStyle')}"
                enabled="false"
                click="_onDeleteClick()"/>

        </s:HGroup>

        <!-- Footer -->
        <s:SkinnableContainer visible="{!!_trim(_statusMsg)}"
            styleName="{getStyle('footerStyle')}"
            width="{Math.max(outerContainer.width || 0, 80) - 10}">

            <!--  Text with details/feed-back about current operation -->
            <s:Label text="{_statusMsg}"
                width="100%"
                lineBreak="toFit"
                maxDisplayedLines="-1"
                />
        </s:SkinnableContainer>

    </s:VGroup>
</s:Group>
